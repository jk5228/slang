# The Slang syntax specification.                           Jason Kim, 7/12/2016

# TODO: allow precedence rules in parser generator?

# Production rules

: num str id + - * / % ! && || == <= >= < > break

prog     : stm*
stmLst   : stm*
stm*     < EMPTY
         | stm+
stm+     < stm
         | stm stm*
stm      < line ;
         | block
line     : exp
         | break
         | retStm
retStm   : return
         | return exp
block    : funBlk
         | ifBlk
         | whileBlk
         | forBlk
funBlk   : def id ( idLst ) { stmLst }
ifBlk    : if ( exp ) { stmLst } else { stmLst }
whileBlk : while ( exp ) { stmLst }
forBlk   : for ( id in exp ) { stmLst }
idLst    : id*
id*      < EMPTY
         | id+
id+      < id
         | id , id+
exp      : ( exp )
         | prim
         | assign
         | arrAcc
         | funExp
         | arrExp
         | arrComp
         | arithExp
         | logExp
prim     : num
         | str
         | id
assign   : id = exp
         | arrAcc = exp
arrExp   : { expLst }
arrComp  : { id in exp : exp }
         | { id in exp : exp -> exp }
expLst   : exp*
exp*     < EMPTY
         | exp+
exp+     < exp
         | exp , exp+
arrAcc   : id [ exp ]
funExp   : id ( expLst )
arithExp : exp aOp2 exp
aOp2     : +
         | -
         | *
         | /
         | %
logExp   : lOp1 exp
         | exp lOp2 exp
lOp1     : !
lOp2     : &&
         | ||
         | ==
         | <=
         | >=
         | >
         | <